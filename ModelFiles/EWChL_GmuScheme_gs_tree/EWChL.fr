(*****************************************************************************************************)
(*            This is the FeynRules model file for the Electroweak Chiral Lagrangian             *)
(*                                                                                                   *)
(*                                                                                                   *)
(*****************************************************************************************************)


(* ************************** *)
(* *****  Information   ***** *)
(* ************************** *)

M$ModelName = "EWChL";

M$Information = {
 Authors      -> {"Author"},
 Version      -> "0.3",
 Date         -> "16.02.2017",
 Institutions -> {"Institution"},
 Emails       -> {"email"},
 URLs         -> "web",
 References   -> {"references"}
};



(* ************************** *)
(* *****  Change  log   ***** *)
(* ************************** *)

(* v0.2: *)
(* Factor of 2 changed in the cVhh vertex so that the SM limit corresponds to cVhh = 1 *)
(* Corrected bug in definition of h^3 and h^4  *)
(* Normalization factor added to h^3 and h^4 terms so that SM limit corresponds now to coupling = 1   *)
(* Added QED order 1 to leading order Higgs couplings because MadGraph complaints when there are couplings without any order.   *)

(* v0.3: *)
(* Lorentz contraction corrected for hVV and hhVV couplings *)


FeynmanGauge = False;

(***** Setting for interaction order (as e.g. used by MadGraph 5)  ******)

M$InteractionOrderLimit = {
 {QL, 1}
};

M$InteractionOrderHierarchy = {
  {QL, 2}
};


(***** Particle classes list ******)

(***** Definition of the physical Higgs particle ******)

M$ClassesDescription = {

 S[1] == { ClassName -> h,
      	   SelfConjugate -> True,
   	   Mass -> {Mh, Internal},
   	   Width -> {Wh, 0.00407},
           ParticleName -> "h",
           PDG -> 5000000,
           PropagatorLabel -> "h",
           PropagatorType  -> D,
           PropagatorArrow -> None,
           FullName -> "h"}
};




(***** Parameter list ******)

M$Parameters = {

 cV == { ParameterType -> External,
         Value -> 1,
         TeX -> Subscript[c,V],
         InteractionOrder -> {QED,1},
         Description -> "Scaling of the Higgs coupling to massive vector bosons"},

 ct == { ParameterType -> External,
         Value -> 1,
         TeX -> Subscript[c,t],
         InteractionOrder -> {QED,1},
         Description -> "Scaling of the top Yukawa"},

 cb == { ParameterType -> External,
	      Value -> 1,
          TeX -> Subscript[c,b],
          InteractionOrder -> {QED,1},
	      Description -> "Scaling of the bottom Yukawa"},

 ctau == { ParameterType -> External,
	   Value -> 1, 
           TeX -> Subscript[c,tau],
           InteractionOrder -> {QED,1},
           Description -> "Scaling of the tau Yukawa"},

 cgaga == { ParameterType -> External,
           Value -> 0,
           TeX -> Subscript[c,gaga],
           InteractionOrder -> {QL,1},
           Description -> "Higgs coupling to photons"},

 cgg == { ParameterType -> External,
	   Value -> 0,
           TeX -> Subscript[c,gg],
           InteractionOrder -> {QED,1},
           Description -> "Higgs coupling to gluons"},

 cZga == { ParameterType -> External,
           Value -> 0,
           TeX -> Subscript[c,Zga],
           InteractionOrder -> {QL,1},
           Description -> "Higgs coupling to Zgamma"},

 cgghh == { ParameterType -> External,
           Value -> 0,
           TeX -> Subscript[c,gghh],
           InteractionOrder -> {QED,2},
           Description -> "Double Higgs coupling to gluons"},

 cVhh == { ParameterType -> External,
       Value -> 1,
       TeX -> Subscript[c,Vhh],
       InteractionOrder -> {QED,1},
       Description -> "Scaling of the double Higgs coupling to massive vector bosons"},

 cthh == { ParameterType -> External,
       Value -> 0,
       TeX -> Subscript[c,thh],
       InteractionOrder -> {QED,1},
       Description -> "Scaling of the double Higgs top Yukawa"},

 chhh == { ParameterType -> External,
       Value -> 1,
       TeX -> Subscript[c,hhh],
       InteractionOrder -> {QED,1},
       Description -> "Triple Higgs coupling"},

 chhhh == { ParameterType -> External,
       Value -> 1,
       TeX -> Subscript[c,hhhh],
       InteractionOrder -> {QED,1},
       Description -> "Quartic Higgs coupling"},

 loop == { ParameterType -> Internal,
	 Value -> 16*Pi^2,
	 TeX -> Subscript[L,f],
         Description -> "loop factor"},

 yyt == { ParameterType -> Internal,
	 Value -> ymt/v,
	 TeX -> Subscript[y,t],
         Description -> "top quark Yukawa coupling"},
		
 yytau == { ParameterType -> Internal,
	 Value -> ymtau/v,
 	 TeX -> Subscript[y,tau],
         Description -> "tau Yukawa coupling"},

 yyb == { ParameterType -> Internal,
	     Value -> ymb/v,
         TeX -> Subscript[y,b],
	     Description -> "bottom Yukawa coupling"},

  v == {
         ParameterType    -> Internal,
         Value            -> 2*MW*sw/ee,
         Description      -> "Higgs VEV"},

  Mh == {
        ParameterType    -> Internal,
        Value            -> 125.0,
        Description      -> "Higgs mass"},

  normh3 == {
        ParameterType -> Internal,
        Value -> Mh*Mh/(2*v*v),
        Description -> "Normalization of Triple Higgs coupling"},

  normh4 == {
        ParameterType -> Internal,
        Value -> Mh*Mh/(8*v*v),
        Description -> "Normalization of Quartic Higgs coupling"}


};


(*****************************************************************************************)
(**************************************** Lagrangian *************************************)
(*****************************************************************************************)

(***** Conventions for the Lagrangian fixed following the Non-Linear EFT section of 1610.07922  ******)


Lhkin := del[h,mu]del[h,mu]/2  - (Mh^2/2) h h;

Lh := (  ee^2/(v loop) cgaga  FS[A,mu,nu] FS[A,mu,nu]
        +  ee g1/(v loop) cZga FS[Z,mu,nu] FS[A,mu,nu]
	    + gs^2/(2 v loop) cgg FS[G,mu,nu,a] FS[G,mu,nu,a]
        +  2 cV/v (1/2 MZ^2 Z[mu] Z[mu] +   MW^2 Wbar[mu] W[mu])
        -  ctau yytau  tabar[sp] ta[sp]   - cb yyb bbar[sp,cc] b[sp,cc]   - ct yyt tbar[sp,cc] t[sp,cc]   ) h;


Lhh := (  gs^2/(2 v^2 loop) cgghh FS[G,mu,nu,a] FS[G,mu,nu,a]
       +  cVhh/v^2 (1/2 MZ^2 Z[mu] Z[mu] +   MW^2 Wbar[mu] W[mu])
       - cthh yyt/v tbar[sp,cc] t[sp,cc]   -  normh3 v chhh h    ) h h;

Lh4 := - normh4 chhhh h h h h;



(*****************************************************************************************)

LagEWChL:= LSM + Lhkin + Lh + Lhh + Lh4;
