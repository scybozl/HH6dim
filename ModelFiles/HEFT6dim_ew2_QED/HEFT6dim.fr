(*****************************************************************************************************)
(*            This is the FeynRules model file for the non-linear effective Higgs Lagrangian         *)
(*                                                                                                   *)
(*                                                                                                   *)
(*****************************************************************************************************)


(* ************************** *)
(* *****  Information   ***** *)
(* ************************** *)

M$ModelName = "HEFT6dim";

M$Information = {
 Authors      -> {"Author"},
 Version      -> "0.3",
 Date         -> "16.02.2017",
 Institutions -> {"Institution"},
 Emails       -> {"email"},
 URLs         -> "web",
 References   -> {"references"}
};



(* ************************** *)
(* *****  Change  log   ***** *)
(* ************************** *)

FeynmanGauge = False;

(***** Setting for interaction order (as e.g. used by MadGraph 5)  ******)

M$InteractionOrderLimit = {
 {QL, 1}
};

M$InteractionOrderHierarchy = {
  {QL, 2}
};


(***** Particle classes list ******)

(***** Definition of the physical Higgs particle ******)

M$ClassesDescription = {

 S[1] == { ClassName -> h,
      	   SelfConjugate -> True,
   	   Mass -> {Mh, Internal},
   	   Width -> {Wh, 0.00407},
           ParticleName -> "h",
           PDG -> 5000000,
           PropagatorLabel -> "h",
           PropagatorType  -> D,
           PropagatorArrow -> None,
           FullName -> "h"}
};




(***** Parameter list ******)

M$Parameters = {

 cV == { ParameterType -> External,
         Value -> 1,
         TeX -> Subscript[c,V],
         InteractionOrder -> {QED,1},
         Description -> "Scaling of the Higgs coupling to massive vector bosons"},

 ct == { ParameterType -> External,
         Value -> 1,
         TeX -> Subscript[c,t],
         InteractionOrder -> {QED,1},
         Description -> "Scaling of the top Yukawa"},

 cb == { ParameterType -> External,
	      Value -> 1,
          TeX -> Subscript[c,b],
          InteractionOrder -> {QED,1},
	      Description -> "Scaling of the bottom Yukawa"},

 ctau == { ParameterType -> External,
	   Value -> 1, 
           TeX -> Subscript[c,tau],
           InteractionOrder -> {QED,1},
           Description -> "Scaling of the tau Yukawa"},

 cgg == { ParameterType -> External,
	   Value -> 0,
           TeX -> Subscript[c,gg],
           InteractionOrder -> {{QL,1},{QED,1}},
           Description -> "Higgs coupling to gluons"},

 cgghh == { ParameterType -> External,
           Value -> 0,
           TeX -> Subscript[c,gghh],
           InteractionOrder -> {{QL,1},{QED,2}},
           Description -> "Double Higgs coupling to gluons"},

 cthh == { ParameterType -> External,
           Value -> 0,
           TeX -> Subscript[c,thh],
           InteractionOrder -> {{QL,1},{QED,2}},
           Description -> "Double Higgs coupling to two tops"},

 chhh == { ParameterType -> External,
       Value -> 1,
       TeX -> Subscript[c,hhh],
       InteractionOrder -> {QED,1},
       Description -> "Triple Higgs coupling"},

 chhhh == { ParameterType -> External,
       Value -> 1,
       TeX -> Subscript[c,hhhh],
       InteractionOrder -> {QED,1},
       Description -> "Quartic Higgs coupling"},

 loop == { ParameterType -> Internal,
	 Value -> 16*Pi^2,
	 TeX -> Subscript[L,f],
         Description -> "loop factor"},

 yyt == { ParameterType -> Internal,
	 Value -> ymt/v,
	 TeX -> Subscript[y,t],
         Description -> "top quark Yukawa coupling"},
		
 yytau == { ParameterType -> Internal,
	 Value -> ymtau/v,
 	 TeX -> Subscript[y,tau],
         Description -> "tau Yukawa coupling"},

 yyb == { ParameterType -> Internal,
	     Value -> ymb/v,
         TeX -> Subscript[y,b],
	     Description -> "bottom Yukawa coupling"},

  v == {
         ParameterType    -> Internal,
         Value            -> 2*MW*sw/ee,
         Description      -> "Higgs VEV"},

  Mh == {
        ParameterType    -> Internal,
        Value            -> 125.0,
        Description      -> "Higgs mass"},

  normh3 == {
        ParameterType -> Internal,
        Value -> 3*Mh*Mh/v,
        Description -> "Normalization of Triple Higgs coupling"},

  normh4 == {
        ParameterType -> Internal,
        Value -> Mh*Mh/(8*v*v),
        Description -> "Normalization of Quartic Higgs coupling"}

};


(*****************************************************************************************)
(**************************************** Lagrangian *************************************)
(*****************************************************************************************)

(***** Conventions for the Lagrangian fixed following the Non-Linear EFT section of 1610.07922  ******)
(***** Factors 1/8 cgg, 1/4 cgghh and 2 cthh to make the equivalence between arXiv:1504.06577   ******)
(***** and the EW Chiral Lagrangian								******)


Lhkin := del[h,mu]del[h,mu]/2  - (Mh^2/2) h h;

Lh := (  - ct yyt tbar[sp,cc] t[sp,cc] +  1/8 cgg aS / (v Pi) FS[G,mu,nu,a] FS[G,mu,nu,a] ) h;


Lhh := (  aS /( 2 v^2 Pi ) 1/4 cgghh FS[G,mu,nu,a] FS[G,mu,nu,a]
        - 2 cthh yyt/(2 v) tbar[sp,cc] t[sp,cc]  ) h h;

Lh3 := - chhh 1/6 normh3 h h h;

Lh4 := - normh4 chhhh h h h h;


(*****************************************************************************************)

LagHEFT6dim := LSM + Lhkin + Lh + Lhh + Lh3 + Lh4;
